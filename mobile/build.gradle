apply plugin: 'com.android.application'
// scala puligin needs to be updated beyond version 1.4, bug report says waiting on release
apply plugin: 'jp.leafytree.android-scala'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize '1G'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        applicationId "com.elliottsj.ftw"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }

    buildTypes {
        debug {
            // 10.0.3.2 is the host IP in a Genymotion VM. Use 10.0.2.2 for the standard android emulator.
            resValue "string", "api_host", "http://10.0.3.2:9000"
        }
        release {
            resValue "string", "api_host", "https://protobus.fasterthanwalking.com"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url 'http://dl.bintray.com/elliottsj/maven'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    compile 'org.scala-lang:scala-library:2.11.4'
    compile 'org.scaloid:scaloid_2.11:3.6.1-10'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.android.support:cardview-v7:21.0.2'
    compile 'com.android.support:recyclerview-v7:21.0.2'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'org.apache.commons:commons-lang3:3.2.1'
    compile 'com.google.guava:guava:18.0'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'com.typesafe.slick:slick_2.11:2.1.0'
    compile 'com.jpardogo.materialtabstrip:library:1.0.7@aar'
    compile 'com.github.johnkil.android-progressfragment:progressfragment-native:1.4.0'
    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.mcxiaoke.volley:library:1.0.9@aar'
    compile 'com.elliottsj:nextbusapi:1.1.2'
    compile 'com.elliottsj:protobus-scala_2.11:0.0.20'
    compile 'com.getpebble:pebblekit:2.6.0'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.additionalParameters = ["-feature"]
}

// Workaround for <https://github.com/saturday06/gradle-android-scala-plugin/issues/42>
afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$rootDir/main-dex-list.txt".toString()
    }
}
